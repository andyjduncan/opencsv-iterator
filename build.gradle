apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'idea'

sourceCompatibility = 1.7
version = '2.0.0'

project.group = 'com.adjectivecolournoun'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}

repositories {
    jcenter()
}

configurations {
    provided
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

dependencies {
    provided "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile "com.opencsv:opencsv:$opencsvVersion"

    testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy'
    }
}

groovydoc {
    groovyClasspath = configurations.provided
    link 'http://opencsv.sourceforge.net/apidocs/', 'au.com.bytecode.opencsv.'
    link 'https://docs.oracle.com/javase/7/docs/api/', 'java.lang.', 'java.util.'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.groovy
    classifier = 'sources'
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    classifier = 'javadoc'
}

task exportJavadoc(type: Copy, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    into 'javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact groovydocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    dryRun = false
    publish = false

    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        name = 'opencsv-iterator'
        desc = 'Iterator wrapper around Open'

        websiteUrl = 'https://github.com/andyjduncan/opencsv-iterator'
        vcsUrl = 'https://github.com/andyjduncan/opencsv-iterator.git'
        issueTrackerUrl = 'https://github.com/andyjduncan/opencsv-iterator/issues'

        licenses = ['Apache-2.0']

        version {
            vcsTag = project.version
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.0"
}

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.provided ]
    }
}