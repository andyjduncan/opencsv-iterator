plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
    id 'idea'
}

sourceCompatibility = '8'
version = '2.0.4'

project.group = 'com.adjectivecolournoun'

repositories {
    jcenter()
}

configurations {
    provided
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

dependencies {
    provided "org.codehaus.groovy:groovy-all:$groovyVersion"
    implementation "com.opencsv:opencsv:$opencsvVersion"

    testImplementation "org.codehaus.groovy:groovy-all:$groovyVersion"
    testImplementation 'org.spockframework:spock-core:2.0-M2-groovy-3.0'
}

test {
    useJUnitPlatform()
}

groovydoc {
    groovyClasspath = configurations.provided
    link 'http://opencsv.sourceforge.net/apidocs/', 'com.opencsv.'
    link 'https://docs.oracle.com/javase/7/docs/api/', 'java.lang.', 'java.util.'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.groovy
    archiveClassifier = 'sources'
}

task groovydocJar(type: Jar, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    archiveClassifier = 'javadoc'
}

task exportJavadoc(type: Copy, dependsOn: 'groovydoc') {
    from groovydoc.destinationDir
    into 'javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact groovydocJar {
                classifier 'javadoc'
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    dryRun = false
    publish = false

    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        name = 'opencsv-iterator'
        desc = 'Iterator wrapper around OpenCSV'

        websiteUrl = 'https://github.com/andyjduncan/opencsv-iterator'
        vcsUrl = 'https://github.com/andyjduncan/opencsv-iterator.git'
        issueTrackerUrl = 'https://github.com/andyjduncan/opencsv-iterator/issues'

        licenses = ['Apache-2.0']

        version {
            vcsTag = project.version
        }
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.provided ]
    }
}
